plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 're.notifica.gradle.notificare-services'
    id 'com.google.gms.google-services'
    id 'com.huawei.agconnect'
}

def properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    namespace 're.notifica.sample'
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "re.notifica.sample.app"
        minSdkVersion min_sdk_version
        targetSdkVersion 31
        versionCode 1
        versionName "3.0.0"

        manifestPlaceholders = [
                googleMapsApiKey: properties.getProperty('google.maps.key'),
        ]
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword properties.getProperty('keystore.debug.store.password')
            keyAlias properties.getProperty('keystore.debug.key.alias')
            keyPassword properties.getProperty('keystore.debug.key.password')
        }
        release {
            storeFile file('release.keystore')
            storePassword properties.getProperty('keystore.release.store.password')
            keyAlias properties.getProperty('keystore.release.key.alias')
            keyPassword properties.getProperty('keystore.release.key.password')
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'api'
    productFlavors {
        apiTest {
            dimension 'api'
            applicationIdSuffix '.test'

            manifestPlaceholders = [
                    notificareApplicationId: properties.getProperty('notificare.test.application.id')
            ]
        }
        apiProduction {
            dimension 'api'

            manifestPlaceholders = [
                    notificareApplicationId: properties.getProperty('notificare.production.application.id')
            ]
        }
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"

    implementation "androidx.appcompat:appcompat:$androidx_app_compat_version"
    implementation "androidx.constraintlayout:constraintlayout:$androidx_constraint_layout_version"
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle_version"
    implementation 'com.google.android.material:material:1.4.0'

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    implementation project(':notificare')
    implementation project(':notificare-assets')
    implementation project(':notificare-authentication')
    implementation project(':notificare-in-app-messaging')
    implementation project(':notificare-inbox')
    implementation project(':notificare-loyalty')

    implementation project(':notificare-geo')
    implementation project(':notificare-geo-gms')
    implementation project(':notificare-geo-hms')
    implementation project(':notificare-geo-beacons')

    implementation project(':notificare-monetize')
    implementation project(':notificare-monetize-gms')

    implementation project(':notificare-push')
    implementation project(':notificare-push-gms')
    implementation project(':notificare-push-hms')

    implementation project(':notificare-push-ui')
    implementation project(':notificare-push-ui-gms')
    implementation project(':notificare-push-ui-hms')

    implementation project(':notificare-scannables')
    implementation project(':notificare-scannables-gms')
    implementation project(':notificare-scannables-hms')
}
