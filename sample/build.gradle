plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 're.notifica.gradle.notificare-services'
}

apply from: '../dependencies.gradle'

def properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def startParameter = getGradle().getStartParameter().getTaskRequests().toString().toLowerCase()
if (startParameter.contains("google")) {
    println("Building flavor with Google GMS - applying plugin")
    apply plugin: 'com.google.gms.google-services'
} else if (startParameter.contains("huawei")) {
    println("Building flavor with Huawei HMS - applying plugin")
    apply plugin: 'com.huawei.agconnect'
}

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "re.notifica.demo"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName "1.0.0"

        manifestPlaceholders = [
                googleMapsApiKey: properties.getProperty('google.maps.key')
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    flavorDimensions "provider"
    productFlavors {
        google {
            dimension "provider"
        }

        huawei {
            dimension "provider"
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation project(':notificare-core')
    implementation project(':notificare-inbox')

    implementation project(':notificare-push')
    googleImplementation project(':notificare-push-fcm')
    huaweiImplementation project(':notificare-push-hms')

    implementation project(':notificare-push-ui')
    googleImplementation project(':notificare-push-ui-fcm')
    huaweiImplementation project(':notificare-push-ui-hms')
}
