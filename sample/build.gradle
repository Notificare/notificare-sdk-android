plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 're.notifica.gradle.notificare-services'
    id 'com.google.gms.google-services'
    id 'com.huawei.agconnect'
}

def properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    namespace 're.notifica.sample'
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "re.notifica.sample.app"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 4
        versionName "3.0.0"

        manifestPlaceholders = [
                googleMapsApiKey: properties.getProperty('google.maps.key'),
        ]
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword properties.getProperty('keystore.debug.store.password')
            keyAlias properties.getProperty('keystore.debug.key.alias')
            keyPassword properties.getProperty('keystore.debug.key.password')
        }
        release {
            storeFile file('release.keystore')
            storePassword properties.getProperty('keystore.release.store.password')
            keyAlias properties.getProperty('keystore.release.key.alias')
            keyPassword properties.getProperty('keystore.release.key.password')
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.dev'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'api'
    productFlavors {
        apiTest {
            dimension 'api'
            applicationId "re.notifica.sample.app.test"
        }
        apiProduction {
            dimension 'api'
        }
    }

    applicationVariants.configureEach {
        if (name == "apiTestDebug") {
            resValue("string", "notificare_app_links_hostname", "\"618d0f4edc09fbed1864e8d0.applinks-test.notifica.re\"")
            resValue("string", "notificare_dynamic_link_hostname", "\"sample-app-dev.test.ntc.re\"")
        } else if (name == "apiTestRelease") {
            resValue("string", "notificare_app_links_hostname", "\"654d017fc468efc19379921e.applinks-test.notifica.re\"")
            resValue("string", "notificare_dynamic_link_hostname", "\"sample-app.test.ntc.re\"")
        } else if (name == "apiProductionDebug") {
            resValue("string", "notificare_app_links_hostname", "\"61644511218adebf72c5449b.applinks.notifica.re\"")
            resValue("string", "notificare_dynamic_link_hostname", "\"sample-app-dev.ntc.re\"")
        } else if (name == "apiProductionRelease") {
            resValue("string", "notificare_app_links_hostname", "\"6511625f445cc1c81d47fd6f.applinks.notifica.re\"")
            resValue("string", "notificare_dynamic_link_hostname", "\"sample-app.ntc.re\"")
        }
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"

    implementation "androidx.appcompat:appcompat:$androidx_app_compat_version"
    implementation "androidx.constraintlayout:constraintlayout:$androidx_constraint_layout_version"
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.datastore:datastore-preferences:$androidx_datastore_version"
    implementation "androidx.fragment:fragment-ktx:$androidx_fragment_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
    implementation("androidx.work:work-runtime-ktx:$androidx_work_version")
    implementation "com.google.android.material:material:$google_material_version"
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    implementation "androidx.navigation:navigation-fragment-ktx:$androidx_navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$androidx_navigation_version"

    implementation project(':notificare')
    implementation project(':notificare-assets')
    implementation project(':notificare-in-app-messaging')
    implementation project(':notificare-inbox')
    implementation project(':notificare-loyalty')

    implementation project(':notificare-geo')
    implementation project(':notificare-geo-gms')
    implementation project(':notificare-geo-hms')
    implementation project(':notificare-geo-beacons')

    implementation project(':notificare-monetize')
    implementation project(':notificare-monetize-gms')

    implementation project(':notificare-push')
    implementation project(':notificare-push-gms')
    implementation project(':notificare-push-hms')

    implementation project(':notificare-push-ui')
    implementation project(':notificare-push-ui-gms')
    implementation project(':notificare-push-ui-hms')

    implementation project(':notificare-scannables')
    implementation project(':notificare-scannables-gms')
    implementation project(':notificare-scannables-hms')
}
