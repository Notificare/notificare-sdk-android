plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

apply from: '../dependencies.gradle'

group = rootProject.ext.maven_artifact_group
version = rootProject.ext.maven_artifact_version

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version

        buildConfigField "String", "SDK_VERSION", "\"${maven_artifact_version.tokenize("-").first()}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"

    // Notificare
    implementation project(':notificare')
    implementation project(':notificare-scannables')

    // Android
    implementation "androidx.appcompat:appcompat:$androidx_app_compat_version"
    implementation "androidx.constraintlayout:constraintlayout:$androidx_constraint_layout_version"
    implementation "androidx.core:core-ktx:$androidx_core_version"

    implementation "com.huawei.hms:base:$huawei_base_version"
    implementation "com.huawei.hms:scan:$huawei_scan_version"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
            }
        }
        repositories {
            maven {
                name 'S3'
                url "s3://maven.notifica.re/${rootProject.ext.maven_artifact_channel}"
                credentials(AwsCredentials) {
                    accessKey rootProject.ext.aws_s3_access_key_id
                    secretKey rootProject.ext.aws_s3_secret_access_key
                }
            }
        }
    }
}
