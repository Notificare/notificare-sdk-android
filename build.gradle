// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'variables.gradle'

    repositories {
        google()
        mavenCentral()
        maven { url 'https://maven.notifica.re/releases' }
        maven { url 'https://developer.huawei.com/repo' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 're.notifica.gradle:notificare-services:1.0.1'
        classpath 'com.google.gms:google-services:4.3.14'
        classpath 'com.huawei.agconnect:agcp:1.7.3.300'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://developer.huawei.com/repo' }
    }
}

subprojects { project ->
    if (project.name == "sample") return

    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'kotlin-parcelize'
    apply plugin: 'maven-publish'

    group = rootProject.ext.maven_artifact_group
    version = rootProject.ext.maven_artifact_version

    def artifact_channel = version ==~ /^([0-9]+)\.([0-9]+)\.([0-9]+)$/ ? 'releases' : 'prereleases'

    task androidSourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from android.sourceSets.main.java.srcDirs
    }

    artifacts {
        archives androidSourcesJar
    }

    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    from components.release
                    artifact androidSourcesJar
                }
            }
            repositories {
                maven {
                    name 'S3'
                    url "s3://maven.notifica.re/$artifact_channel"
                    credentials(AwsCredentials) {
                        accessKey rootProject.ext.aws_s3_access_key_id
                        secretKey rootProject.ext.aws_s3_secret_access_key
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
