plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'maven-publish'
}

apply from: '../dependencies.gradle'

group = rootProject.ext.maven_artifact_group
version = rootProject.ext.notificare_push_hms_version_name

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version

        buildConfigField "String", "SDK_VERSION", "\"${notificare_push_hms_version_name.tokenize("-").first()}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // HMS Push depends on a previous version of kotlin-reflect.
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation project(':notificare')
    implementation project(':notificare-push')

    implementation "com.huawei.hms:push:$huawei_push_version"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
            }
        }
        repositories {
            maven {
                name 'S3'
                url "s3://maven.notifica.re/${rootProject.ext.maven_artifact_channel}"
                credentials(AwsCredentials) {
                    accessKey rootProject.ext.aws_s3_access_key_id
                    secretKey rootProject.ext.aws_s3_secret_access_key
                }
            }
        }
    }
}
