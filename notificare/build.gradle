plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'maven-publish'
}

apply from: '../dependencies.gradle'

group = rootProject.ext.maven_artifact_group
version = rootProject.ext.maven_artifact_version

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version

        buildConfigField "String", "SDK_VERSION", "\"${maven_artifact_version.tokenize("-").first()}\""

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true",
                ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Android
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.work:work-runtime-ktx:$androidx_work_version"

    // Android: Room
    implementation "androidx.room:room-runtime:$androidx_room_version"
    implementation "androidx.room:room-ktx:$androidx_room_version"
    kapt "androidx.room:room-compiler:$androidx_room_version"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // Moshi
    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
            }
        }
        repositories {
            maven {
                name 'S3'
                url "s3://maven.notifica.re/${rootProject.ext.maven_artifact_channel}"
                credentials(AwsCredentials) {
                    accessKey rootProject.ext.aws_s3_access_key_id
                    secretKey rootProject.ext.aws_s3_secret_access_key
                }
            }
        }
    }
}
